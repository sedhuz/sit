plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:32.0.1-android'
    testImplementation 'junit:junit:4.13.2'
}


tasks.jar {
    manifest {
        attributes 'Main-Class': 'com.infinull.sit.App'
    }
}

tasks.test {
    dependsOn tasks.cleanTest

    testLogging {
        events "passed", "skipped"
        exceptionFormat = "full"
    }

    afterTest { descriptor, result ->
        if (result.resultType == TestResult.ResultType.FAILURE) {
            def RED = "\u001B[31m"
            def RESET = "\u001B[0m"
            println "\n============> TEST FAILED <=============="
            print("${RED}")
            println "NAME   : ${descriptor.name}"
            if (result.exception != null) {
                def exception = result.exception
                println "REASON : ${exception.message}"
            }
            print("${RESET}")
            println "==========================================="
        }
    }

    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
            def repeatLength = output.length() + 6
            println('\n' + ('=' * repeatLength) + '\n   ' + output + '\n' + ('=' * repeatLength))
        }
    }
}

mainClassName = 'com.infinull.sit.App'